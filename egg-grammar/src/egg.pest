// Identifier to name types, variables, functions, etc.
Identifier = @{ RegularIdentifier | StringIdentifier }

// Identifier without special characters.
RegularIdentifier = @{ !Keyword ~ ASCII_ALPHA ~ ("_" | "-" | ASCII_ALPHANUMERIC)* ~ !RegularStringLiteral }

// Identifier that can contain any character.
StringIdentifier = @{ "id" ~ RegularStringLiteral }

// Keyword to exclude from RegularIdentifier.
Keyword = @{
    | "const"
    | "exec"
    | "exit"
    | "fn"
    | "if"
    | "let"
    | "pub"
    | "return"
    | TypeKeyword
    | LiteralKeyword
}

// Name of a primitive type.
TypeKeyword = @{
    | "any"
    | "never"
    | "void"
    | NumTypeKeyword
}

// Name of a numeric type.
NumTypeKeyword = @{ IntTypeKeyword | FloatTypeKeyword }

// Name of an integer type.
IntTypeKeyword = @{ ("u" | "i") ~ ("8" | "16" | "32" | "64" | "128") }

// Name of a floating-point number type.
FloatTypeKeyword = @{ "f32" | "f64" }

// Literal keyword to exclude from RegularIdentifier.
LiteralKeyword = @{
    | "true"
    | "false"
    | "null"
    | "inf"
    | "nan"
}

// String literal.
StringLiteral = @{ RegularStringLiteral | RawStringLiteral }

// String literal with escape sequences.
RegularStringLiteral = ${
    | ("\"" ~ (!"\"" ~ RegularCharacter)* ~ "\"")
    | ("'" ~ (!"'" ~ RegularCharacter)* ~ "'")
}

// Characters to used in `RegularStringLiteral`.
RegularCharacter = @{
    | !"\\" ~ ANY
    | "\\" ~ ("\"" | "'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ "x" ~ ASCII_HEX_DIGIT{2}
    | "\\" ~ "u" ~ ASCII_HEX_DIGIT{4}
    | "\\" ~ "u" ~ "{" ~ ASCII_HEX_DIGIT+ ~ "}"
}

// String literal without escape sequences.
RawStringLiteral = @{ "r" ~ RawStringLiteralBody }

// Body of a raw string.
RawStringLiteralBody = @{
    | ("\"" ~ (!"\"" ~ ANY)* ~ "\"")
    | ("'" ~ (!"'" ~ ANY)* ~ "'")
}

// Ident at the start of a line to define a block.
Indentation = _{ (" " | "\t")* }

// Implicit whitespace
WHITESPACE = _{ (" " | "\t")+ }
