// Identifier without special characters.
Identifier = @{ !Keyword ~ ASCII_ALPHA ~ ("_" | "-" | ASCII_ALPHANUMERIC)* }

// Keyword to exclude from Identifier.
Keyword = @{
    | "const"
    | "exec"
    | "exit"
    | "fn"
    | "if"
    | "let"
    | "pub"
    | "return"
    | TypeKeyword
    | LiteralKeyword
}

// Name of a primitive type.
TypeKeyword = @{
    | "any"
    | "never"
    | "void"
    | NumTypeKeyword
}

// Name of a numeric type.
NumTypeKeyword = @{ IntTypeKeyword | FloatTypeKeyword }

// Name of an integer type.
IntTypeKeyword = @{ ("u" | "i") ~ ("8" | "16" | "32" | "64" | "128") }

// Name of a floating-point number type.
FloatTypeKeyword = @{ "f32" | "f64" }

// Literal keyword to exclude from Identifier.
LiteralKeyword = @{
    | "true"
    | "false"
    | "null"
    | "inf"
    | "nan"
}

// String literal with escape sequences.
BareStringLiteral = ${
    | ("\"" ~ RegularCharacter* ~ "\"")
    | ("'" ~ RegularCharacter* ~ "'")
}

// Characters to used in `BareStringLiteral`.
RegularCharacter = @{
    | !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ "x" ~ ASCII_HEX_DIGIT{2}
    | "\\" ~ "u" ~ ASCII_HEX_DIGIT{4}
    | "\\" ~ "u" ~ "{" ~ ASCII_HEX_DIGIT+ ~ "}"
}

// Ident at the start of a line to define a block.
Indentation = _{ (" " | "\t")* }

// Implicit whitespace
WHITESPACE = _{ (" " | "\t")+ }
